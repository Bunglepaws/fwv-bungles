scn fwvTCmonitorSCRIPT

short activated
short tutOnce
ref rLinkedRef
Begin OnActivate player

	set rLinkedRef to GetLinkedRef
	set activated to 1

	;alter TARDIS Terminal Header
	con_setgamesetting sComputersHeader1 "T.A.R.D.O.S"
	con_setgamesetting sComputersHeader2 "Time And Relative Dimensional Operating System"
	con_setgamesetting sHackingHeader "============================================"
	con_setgamesetting shackingintro01 " "
	con_setgamesetting shackingintro06 " "
	con_setgamesetting shackingintro09 " "

	if fwvqGameOfRassilon.sRemote == 0
		if rLinkedRef == 0
			if fwvqTutorials.tutorial == 1 && tutOnce == 0
				ShowTutorialMenu fwvTUTmonitorMessage
				set tutOnce to 1
			endif
	
			if fwvqSystems.core == 1 || fwvqGameOfRassilon.sRemote == 0
				fwvTmonitorRef.activate player 1
			elseif fwvqSystems.core != 1
				MessageEx "[The TARDIS is without power]"
			endif
		endif
	elseif fwvqGameOfRassilon.sRemote == 1
		MessageBoxEx "[The Time Lords have control of the TARDIS]"
	endif

end

Begin MenuMode
  if activated == 1
    set activated to 2
  endif
End

Begin GameMode
  if activated == 2
    set activated to 0
    ;On shutting down the terminal. Makes sure it doesn't show up on others.
    con_setgamesetting sComputersHeader1 "ROBCO INDUSTRIES UNIFIED OPERATING SYSTEM"
    con_setgamesetting sComputersHeader2 "COPYRIGHT 2075-2077 ROBCO INDUSTRIES "
    con_setgamesetting sHackingHeader "ROBCO INDUSTRIES (TM) TERMLINK PROTOCOL"
    con_setgamesetting shackingintro01 "WELCOME TO ROBCO INDUSTRIES (TM) TERMLINK"
    con_setgamesetting shackingintro06 "Initializing Robco Industries(TM) MF Boot Agent v2.3.0"
    con_setgamesetting shackingintro09 "Copyright 2201-2203 Robco Ind."
  endif
End

