scn fwvTfoodMachineScript

float zAngle
short direction
short doOnce
short countTotal
short count
float dispenseTimer
float randDist
ref linkRef
ref curRef
ref cube1
ref cube2
ref cube3
begin OnActivate Player
	if doOnce == 0
		set zAngle to GetAngle Z
		set zAngle to fmod zAngle 360
		set direction to zAngle / 90
		set linkRef to GetLinkedRef
		set doOnce to 1
		set countTotal to 18
		set dispenseTimer to 0
		linkRef.PlaceAtMe fwvExpControlExplosion 1
		PlaySound3d fwvFXfoodProcessorBeep
	elseif countTotal == 0
		set countTotal to 3
		set count to 0
		set dispenseTimer to 0
		PlaySound3d fwvFXfoodProcessorBeep
	endif
	activate
end

begin GameMode
	if countTotal > 0
		set dispenseTimer to dispenseTimer + GetSecondsPassed
		if dispenseTimer > 0.25 && count == 0
			set dispenseTimer to 0
			set count to 3
			
			set cube1 to linkRef.PlaceAtMe fwvItemCubeFood 1
			set cube2 to linkRef.PlaceAtMe fwvItemCubeFood 1
			set cube3 to linkRef.PlaceAtMe fwvItemCubeFood 1
			
			set countTotal to countTotal - 3
			PlaySound fwvFXfoodProcessorSpawn
		elseif count == 3
			set count to 0
		
			Label 0
			
			if count == 0
				set curRef to cube1
			elseif count == 1
				set curRef to cube2
			elseif count == 2
				set curRef to cube3
			endif
			
			set randDist to 150 + GetRandomPercent
			if direction == 0 ; Y -
				set randDist to 0-randDist
				curRef.con_SetVel Y randDist
			elseif direction == 1 ; X -
				set randDist to 0-randDist
				curRef.con_SetVel X randDist
			elseif direction == 2 ; Y +
				curRef.con_SetVel Y randDist
			else ; direction == 3 ; X +
				curRef.con_SetVel X randDist
			endif
			
			set count to count + 1
			
			if count < 3
				Goto 0
			endif
			
			set count to 0
		endif
	endif
end
