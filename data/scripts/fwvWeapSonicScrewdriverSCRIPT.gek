scn fwvWeapSonicScrewdriverSCRIPT

ref targetCell
ref CombTarget
ref Weap
ref rPoliceBox
ref rPoliceBoxDoor
int WeapSkill
ref rConsoleRoom
short sIsRobot
short sType
short sMode
short sRefType
short sLoops
short sIndex
short sMaxIndex
float fLFHealthPerc
ref	rSelf
ref rTarget

ref FastRetRef
ref FastRetTarget

float fPz
float fMx
float fMy
float fMz
float DoorAngle

float fCalc
float fCalc2
short sFadeOutIn

begin OnEquip Player
	set fwvq1.fQuestDelayWeapon to 0.1

end

begin onUnequip Player
	setnumericgamesetting iActivatePickLength 150	
end

begin onFire
	set rConsoleRoom to fwvq1.rTDesktopRoom
	set CombTarget to GetCrosshairRef
	set sType to GetObjectType CombTarget
	set targetCell to combtarget.getTeleportCell

	if fwvqWeaponQuest.sonicMode == 1		; ========= LOCKPICK MODE

		if sType == 28 || sType == 27
			if CombTarget.GetLocked == 1
				if CombTarget.GetLockLevel <= 100
					PlaySound UILockpickingUnlock
					CombTarget.Unlock

					if targetCell == 0

						;CombTarget.Activate Player 1

					else

						CombTarget.setOpenState 1

					endif

				else
					MessageEx "[You cannot open a Deadlock Seal...]"
				endif
			elseif CombTarget.GetLocked == 0
				PlaySound UILockpickingUnlock
				CombTarget.setOpenState 0
				CombTarget.lock 100
			endif
		endif

	elseif fwvqWeaponQuest.sonicMode == 2	; ========= HACKING MODE

		if sType == 23
			if CombTarget.GetLockLevel <= 500
					PlaySound UIHackingPassGood
					CombTarget.Unlock
			endif
		endif
		
		if CombTarget == fwvTelosSensor01 || CombTarget == fwvTelosSensor02 || CombTarget ==  fwvTelosSensor03 || CombTarget ==  fwvTelosSensor04 || CombTarget == fwvAutonMurderTransmitterRef
			CombTarget.Activate Player 1
		endif

		if CombTarget.GetIsID fwvMoonJunctionBox == 1
			printc "MoonJuncBox Targeted"
			CombTarget.do 8
		endif
		
		if CombTarget == fwvCyberConversionMFREF
			if fwvqInvasions.CyberPrisoner01Uncrux == 0
				PlaySound OBJForcefieldShutdown
				set fwvqInvasions.CyberPrisoner01Uncrux to 1
				
				fwvCybStrngPrisoner01NPC.addscriptpackage fwvCyberStrongholdPrisonerFreeWaitPackage1
				fwvCybStrngPrisoner02NPC.addscriptpackage fwvCyberStrongholdPrisonerFreeWaitPackage2
				fwvCybStrngPrisoner03NPC.addscriptpackage fwvCyberStrongholdPrisonerFreeWaitPackage3
				fwvCybStrngPrisoner04NPC.addscriptpackage fwvCyberStrongholdPrisonerFreeWaitPackage4
				
				fwvCybStrngPrisoner01NPC.moveto fwvCybStrngPrisoner01GoToPrep
				fwvCybStrngPrisoner02NPC.moveto fwvCybStrngPrisoner02GoToPrep
				fwvCybStrngPrisoner03NPC.moveto fwvCybStrngPrisoner03GoToPrep
				fwvCybStrngPrisoner04NPC.moveto fwvCybStrngPrisoner04GoToPrep

				fwvCybStrngPrisoner01NPC.evp
				fwvCybStrngPrisoner02NPC.evp
				fwvCybStrngPrisoner03NPC.evp
				fwvCybStrngPrisoner04NPC.evp
			
				MessageEx "[Subjects released, Cyberconversion aborted]"
			endif
		endif
		
		if CombTarget.GetIsCreatureType 6 == 1 && CombTarget.getInFaction fwvCybermanFaction == 0 && CombTarget.getInFaction fwvDalekFaction == 0

			;PlaySound sonicScrewShoot
			CombTarget.setAV aggression 0
			CombTarget.FriendDispositionBoost

		endif

		if CombTarget.getInFaction fwvCybermanFaction == 1 || CombTarget.getInFaction fwvDalekFaction == 1
			MessageEx "[You cannot hack cyborgs as you can robots...]"
		endif
		
	elseif fwvqWeaponQuest.sonicMode == 3 ; ========== SCAN LIFE

		set sLoops to 0
			Label 0
				set sLoops to sLoops + 1
				if sLoops == 1
					set sRefType to 42
				elseif sLoops == 2
					set sRefType to 43
				endif
				set sIndex to 0
				set sMaxIndex to GetNumRefs sRefType 1
				if sMaxIndex > 0
					set sIndex to sIndex + 1
					set CombTarget to GetFirstRef sRefType 2
					Label 1
						; do stuff to CombTarget here
						if CombTarget.GetDistance Player <= 1024
							set fLFHealthPerc to CombTarget.GetHealthPercentage
							if CombTarget.GetKnockedState == 1
								CombTarget.pms fwvHologramHealthRed 2.5
							elseif fLFHealthPerc < 0.33
								CombTarget.pms fwvHologramHealthRed 2.5
							elseif fLFHealthPerc < 0.66
								CombTarget.pms fwvHologramHealthYellow 2.5
							elseif fLFHealthPerc < 0.93
								CombTarget.pms fwvHologramHealthGreen 2.5
							elseif CombTarget.GetDead == 1								
								CombTarget.pms fwvHologramHealthRed 2.5
							else
								CombTarget.pms fwvHologramHealthBlue 2.5
							endif
						endif
						
						if sMaxIndex > sIndex
							set sIndex to sIndex + 1
							set CombTarget to GetNextRef
							GoTo 1
						endif
					; Label 1 end
				endif
				if sLoops < 2
					GoTo 0
				endif
			; Label 0 end

	elseif fwvqWeaponQuest.sonicMode == 4 ; ========== SCAN VITALS ;no this is gone

		set CombTarget to Player.GetCombatTarget

		if CombTarget.IsActor || CombTarget.GetIsCreature
			set fLFHealthPerc to CombTarget.GetHealthPercentage * 100
			;ShowMessage fwvSonicScanHp fLFHealthPerc
		endif

	elseif fwvqWeaponQuest.sonicMode == 5 ; ========== DISARM MODE

		set CombTarget to Player.GetCombatTarget
		if CombTarget.IsActor || CombTarget.GetIsCreature
			if CombTarget.getInFaction fwvAutonFaction == 0 && CombTarget.getInFaction fwvCybermanFaction == 0 && CombTarget.getInFaction fwvDalekFaction == 0
				set Weap to CombTarget.GetEquippedObject 5
				CombTarget.ModWeaponHealthPerc -10
				CombTarget.Drop Weap 1
				CombTarget.PlaySound3D FXExplosionGrenadeEMPHigh
			else
				MessageEx "The Sonic Screwdriver cannot disarm alien technology..."
			endif
		endif

	elseif fwvqWeaponQuest.sonicMode == 6 ; ========== TARDIS RECALL MODE
		if fwvq1.tRepair != 2 && fwvq1.tRefuel != 2
			if GameDaysPassed + 0.5 < fwvqWeaponQuest.tardisCooldown
				set fwvqWeaponQuest.tardisCooldown to 0.0 ; reset incase count gets messed up
			endif
			if fwvqSystems.tAdvanced == 0
				if GameDaysPassed > fwvqWeaponQuest.tardisCooldown + 0.5  ;XO note - 12 hours? 
					if player.isSwimming == 0 && Player.GetInCell rConsoleRoom == 0 && Player.GetInCell fwvAngelMotel01 == 0 && Player.GetInCell fwvTinterior1 == 0 && fwvTardisRecallExtFadeInBox.fTimeFade <= 0.0 && fwvq1.sPoliceBoxIsUnspawned == 0
						set fwvqWeaponQuest.tardisCooldown to GameDaysPassed
						
						set rPoliceBox to fwvq1.rPoliceBoxRef
						set rPoliceBoxDoor to fwvq1.rPoliceBoxDoorRef
						
						let FastRetRef := fwvFastReturnRef
						let FastRetTarget := fwvq1.enableDoor 
						FastRetRef.moveto FastRetTarget
						set DoorAngle to FastRetTarget.GetAngle Z
						FastRetRef.SetAngle Z DoorAngle

						;rPoliceBox.Disable 0
						ShowMessage fwvWeapSonicRecallMessage

						; teleport the exterior TARDIS stuff
						set CombTarget to Player ;fwvq1.enableDoor

						set fPz to CombTarget.GetAngle Z
						set fMx to Sin fPz
						set fMy to Cos fPz
						set fMx to fMx * 96 + CombTarget.GetPos X
						set fMy to fMy * 96 + CombTarget.GetPos Y
						set fMz to CombTarget.GetPos Z

						set fPz to fPz + 180
						
						PoliceBoxAltLocationRef.MoveTo Player
						Call fwvFuncSetPosition { PoliceBoxAltLocationRef fMx fMy fMz }
						PoliceBoxAltLocationRef.SetAngle Z fPz
						
						let fwvqWeaponQuest.sTardisRecallFrame := 1
						
					else
						PlaySound WPNRechargerFail
						ShowMessage fwvWeapModeUnavailableMessage
					endif
				else
					PlaySound WPNRechargerFail
					ShowMessage fwvWeapModeUnavailableMessage
				endif
			else
				PlaySound WPNRechargerFail
				MessageEx "Not Available in Advanced Mode"
			endif
		;else
		;	PlaySound WPNRechargerFail
		;	ShowMessage fwvWeapCooldownMessage
		endif
	endif

end

Begin Gamemode

	;set the range of the sonic as defined in the menu
	if Player.IsWeaponInList fwvWSonicList == 1
		if player.isWeaponOut == 1	
			if fwvqWeaponQuest.sonicRange < 1
				setnumericgamesetting iActivatePickLength 150
			else
				setnumericgamesetting iActivatePickLength 1000
			endif
		else
			setnumericgamesetting iActivatePickLength 150
		endif
	else
		setnumericgamesetting iActivatePickLength 150
	endif



End
